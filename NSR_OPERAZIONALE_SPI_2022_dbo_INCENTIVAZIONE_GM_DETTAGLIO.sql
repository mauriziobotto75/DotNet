USE [NSR_Operazionale_BF_2022]
GO
/****** Object:  StoredProcedure [Classifica].[PR_INCENTIVAZIONE_GM]    Script Date: 18/07/2022 15:24:51 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*
 ============================================================================
 Autore:			CAPGEMINI
 Data Creazione:	27/04/2018
 Descrizione:		Calcolo Incentivazione GM
 ============================================================================
 Parametri:
 ============================================================================
 Variazioni
 ============================================================================
 Ver.	Data        Autore  Descrizione
 ----	----------  ------  -------------------------------------------------
	1	30/06/2022  DM      Primo Rilascio
 ============================================================================
 Comando: 
	EXEC Classifica.PR_INCENTIVAZIONE_GM
 ============================================================================
*/
ALTER PROCEDURE [Classifica].[PR_INCENTIVAZIONE_GM]
AS
BEGIN
	SET NOCOUNT ON

	DECLARE @Anno INT,
		    @DataStorfa INT,
			@Rete VARCHAR(3),
			@DataInizioAnno INT,
			@dataInizioNewEntry INT,
			@DataScadenzaIncentivazione INT,
			@DataStruttura INT,
			@eta INT,
			@categorieEsclusioneNext VARCHAR(100),
			@NumeroPBNext INT,
			@PercentualeConformitaNext INT,
			@SogliaRFANext NUMERIC(38, 2),
			@NumeroPBOld INT,
			@PercentualeConformitaOld INT,
			@SogliaRFAOld NUMERIC(38, 2)
			
	SELECT 
		@dataInizioNewEntry = IIF(CHIAVE = 'DATA_INIZIO_NEW_ENTRY' AND CODICE = '', VALORE, @dataInizioNewEntry),
		@DataScadenzaIncentivazione = IIF(CHIAVE = 'DATA_SCADENZA_INCENTIVAZIONE' AND CODICE = '', VALORE, @DataScadenzaIncentivazione),
		@DataStruttura = IIF(CHIAVE = 'DATA_STRUTTURA' AND CODICE = '', VALORE, @DataStruttura),
		@eta = IIF(CHIAVE = 'ETA' AND CODICE = '', VALORE, @eta),
		@categorieEsclusioneNext = IIF(CHIAVE = 'CATEGORIA_ESCLUSIONE' AND CODICE = 'GM_NEXT', VALORE, @categorieEsclusioneNext),
		@NumeroPBNext = IIF(CHIAVE = 'CF_CANCELLO' AND CODICE = 'GM_NEXT', VALORE, @NumeroPBNext),
		@PercentualeConformitaNext = IIF(CHIAVE = 'CONFORMITA_CANCELLO' AND CODICE = 'GM_NEXT', VALORE, @PercentualeConformitaNext),
		@SogliaRFANext = IIF(CHIAVE = 'RFA_CANCELLO' AND CODICE = 'GM_NEXT', VALORE, @SogliaRFANext),
		@NumeroPBOld = IIF(CHIAVE = 'CF_CANCELLO' AND CODICE = 'GM_OLD', VALORE, @NumeroPBOld),
		@PercentualeConformitaOld = IIF(CHIAVE = 'CONFORMITA_CANCELLO' AND CODICE = 'GM_OLD', VALORE, @PercentualeConformitaOld),
		@SogliaRFAOld = IIF(CHIAVE = 'RFA_CANCELLO' AND CODICE = 'GM_OLD', VALORE, @SogliaRFAOld)
	FROM
		Classifica.CONFIGURAZIONE_INCENTIVAZIONE_GM cig (NOLOCK)
	
	SELECT 
		@Anno = ANNO,
		@DataInizioAnno = DATA_INIZIO,
		@DataStorfa = NSRFrontendWebMultiRete.Comune.fnGetNumericFromDate(DATEADD(DAY, -1, NSRFrontendWebMultiRete.Comune.fnGetDateFromNumeric(DATA_INIZIO))),
		@Rete = RETE
	FROM 
		dbo.LOCAL_ID_REG_RC (NOLOCK)

	IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'Classifica.Temp_PARTECIPANTI_INCENTIVAZIONE_GM') AND type in (N'U'))
	BEGIN
		DROP TABLE Classifica.Temp_PARTECIPANTI_INCENTIVAZIONE_GM
	END

	PRINT(@DataStruttura)
	PRINT(@Anno)

-- CREO UNA TABELLA TEMPORANEA PER ELABORARE I PARTECIPANTI DELL'INCENTIVAZIONE COMPRE
	SELECT
		GAW.CODAGE80,
		GAW.CODAGE70,
		GAW.CODAGE60, 
		GAW.CODAGE50S AS CODAGE50,
		A.CODAGE,
		
		CASE
			WHEN ISNULL(A.DTASCAINC, 0) >= @DataInizioAnno
			THEN 'PB_RES'
			ELSE 'PB'
		END AS TIPO_PB,		
		
		A.STATUS,
		A.DTASCAINC,
		
		CASE 
			WHEN GAW.CODAGE50S <> ISNULL(GA.CODAGE50, '') THEN 6
			WHEN A.DTASCAINC > @DataScadenzaIncentivazione THEN 1
			WHEN A.COD_CATEG = 'A' THEN 2
			WHEN A.FLAG_HA_AZIENDA = 1 THEN 3
			WHEN A.FLAG_HA_AREA = 1 THEN 4
			WHEN NM.FLAG_VALIDO = 1 THEN 5
			ELSE 0
		END FLAG_ESCLUSIONE,
		
		0 AS FLAG_ECCEZIONE
	INTO Classifica.Temp_PARTECIPANTI_INCENTIVAZIONE_GM
	FROM
		dbo.GERARCHIA_AGENTE_WEB GAW (NOLOCK)
		INNER JOIN dbo.AGENTE a50 (NOLOCK) ON	GAW.CODAGE50S = a50.CODAGE
		LEFT JOIN DBO.GERARCHIA_AGENTE GA (NOLOCK) ON GA.CODAGE = GAW.CODAGE
											 AND GA.ANNO = @ANNO
											 AND GA.TIPO_GERARCHIA = 'RT'
		LEFT JOIN dbo.AGENTE A (NOLOCK) ON GAW.CODAGE = A.CODAGE
		LEFT JOIN dbo.AGENTE_NEW_MAN NM (NOLOCK) ON A.CODAGE = NM.CODAGE
													AND NM.DATINPRO >= @dataInizioNewEntry
	WHERE
		GAW.DRENDICON = @DataStruttura
		AND GAW.TIPO_GERARCHIA = 'RT' 
		AND GAW.ANNO = @Anno 
		AND GAW.CODAGE50S NOT LIKE 'I%' 
		AND ISNULL(GAW.CODAGE50S, '') <> ''
		AND GAW.CODAGE50S <> GA.CODAGE 
		AND GAW.STATO = 'A' 
		AND GAW.GRADO < '050'

	INSERT INTO Classifica.Temp_PARTECIPANTI_INCENTIVAZIONE_GM
	(
		CODAGE80,
		CODAGE70,
		CODAGE60, 
		CODAGE50,
		CODAGE,
		TIPO_PB,
		STATUS,
		DTASCAINC,
		FLAG_ESCLUSIONE,
		FLAG_ECCEZIONE
	)
	SELECT
		ISNULL(GA.CODAGE80, ''),
		ISNULL(GA.CODAGE70, ''),
		ISNULL(GA.CODAGE60, ''),
		ISNULL(GA.CODAGE50S, ''),
		A.CODAGE,
		CASE
			WHEN ISNULL(A.DTASCAINC, 0) >= @DataInizioAnno
			THEN 'PB_RES'
			ELSE 'PB'
		END AS TIPO_PB,
		A.STATUS,
		A.DTASCAINC,
		GM.FLAG_ESCLUSIONE,
		1 AS FLAG_ECCEZIONE
	FROM
		Classifica.Temp_PARTECIPANTI_INCENTIVAZIONE_GM D (NOLOCK)
		RIGHT JOIN dbo.INCENTIVAZIONE_GM_ECCEZIONE GM (NOLOCK) ON GM.CODAGE =  D.CODAGE
		LEFT JOIN dbo.AGENTE A (NOLOCK) ON	GM.CODAGE = A.CODAGE
		LEFT JOIN dbo.AGENTE_NEW_MAN NM (NOLOCK) ON A.CODAGE = NM.CODAGE
		LEFT JOIN dbo.GERARCHIA_AGENTE_WEB GA (NOLOCK) ON GA.CODAGE = D.CODAGE
	WHERE
		GM.CODAGE IS NOT NULL AND
		D.CODAGE IS NULL


  /*Creo la tabella INCENTIVAZIONE_GM_DETTAGLIO 
	Se FLAG_PB_A_BONUS = 1 ha i requisiti per essere PB a bonus
  */
	IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'Classifica.INCENTIVAZIONE_GM_DETTAGLIO') AND type in (N'U'))
	BEGIN
		DROP TABLE Classifica.INCENTIVAZIONE_GM_DETTAGLIO
	END

	;WITH CTE_AGENTE
	AS
	(
		SELECT
			CODAGE80,
			CODAGE70,
			CODAGE60, 
			CODAGE50,
			CODAGE,
			STATUS,
			DTASCAINC,
			TIPO_PB,
			FLAG_ESCLUSIONE,
			FLAG_ECCEZIONE
		FROM
			Classifica.Temp_PARTECIPANTI_INCENTIVAZIONE_GM (NOLOCK)
	),
	CTE_OBIETTIVO
	AS
	(
		SELECT
			O.CODAGE,
			O.IMP_OBIETTIVO,
			DO.CATEGORIA_PB
		FROM 
			dbo.OBIETTIVI O (NOLOCK)
			LEFT JOIN dbo.DECODIFICA_OBIETTIVI_RUOLO_RM DO (NOLOCK) ON O.ID_CATEGORIA_PB = DO.ID_CATEGORIA_PB
		WHERE
			O.COD_MISURA = 'RNT'
			AND O.SIGLA_INC = 'C'
	)	
	SELECT
		A.CODAGE80,
		A.CODAGE70,
		A.CODAGE60, 
		A.CODAGE50,
		A.CODAGE,
		A.STATUS,
		A.DTASCAINC,
		A.TIPO_PB,
		ISNULL(O.CATEGORIA_PB, '') AS CATEGORIA_PB,
		ISNULL(CAP.IMP_CAP, 0) AS IMP_CAP,
		ISNULL(O.IMP_OBIETTIVO, 0) AS IMP_OBIETTIVO,
		CAST(0 AS NUMERIC(15, 2)) AS IMP_RFA,
		
		ISNULL(CAP_RNT.IMP_CAP, 0) AS IMP_CAP_GM,
		ISNULL(CAP_RNT.IMP_TOTALE_VAL_RT, 0) AS IMP_TOTALE_VAL_RT_GM,
		ISNULL(CAP_RNT.IMP_TOTALE_BVL_RT, 0) AS IMP_TOTALE_BVL_RT_GM,
		ISNULL(CAP_RNT.IMP_TOTALE_RNT_RT, 0) AS IMP_TOTALE_RNT_RT_GM,
		ISNULL(CAP_RNT.IMP_TOTALE_RNG_RT, 0) AS IMP_TOTALE_RNG_RT_GM,

		ISNULL(CAP.IMP_TOTALE_VAL_RT, 0) AS IMP_TOTALE_VAL_RT,
		ISNULL(CAP.IMP_TOTALE_BVL_RT, 0) AS IMP_TOTALE_BVL_RT,
		ISNULL(CAP.IMP_TOTALE_RNT_RT, 0) AS IMP_TOTALE_RNT_RT,
		ISNULL(CAP.IMP_TOTALE_RNG_RT, 0) AS IMP_TOTALE_RNG_RT,

		CASE
			WHEN A.TIPO_PB = 'PB'
			THEN ISNULL(CAP.IMP_CAP, 0)
			ELSE 0
		END AS IMP_CAP_RES,

		CASE
			WHEN A.TIPO_PB = 'PB'
			THEN ISNULL(CAP.IMP_TOTALE_VAL_RT, 0)
			ELSE 0
		END AS IMP_TOTALE_VAL_RES,

		CASE
			WHEN A.TIPO_PB = 'PB'
			THEN ISNULL(CAP.IMP_TOTALE_BVL_RT, 0)
			ELSE 0
		END AS IMP_TOTALE_BVL_RES,
		
		CASE
			WHEN A.TIPO_PB = 'PB'
			THEN ISNULL(CAP.IMP_TOTALE_RNT_RT, 0)
			ELSE 0
		END AS IMP_TOTALE_RNT_RES,
		
		CASE
			WHEN A.TIPO_PB = 'PB'
			THEN ISNULL(CAP.IMP_TOTALE_RNG_RT, 0)
			ELSE 0
		END AS IMP_TOTALE_RNG_RES,

		ISNULL(C.QAV_CLIENTI_TOTALI, 0) AS QAV_ATTUALE_CLIENTI,
		ISNULL(C.QAV_CLIENTI_VALIDI, 0) AS QAV_ATTUALE_SINTESI,
		ISNULL(C.QAV_PERCENTUALE, 0) AS QAV_PERC,

		ISNULL(C.VAR_CLIENTI_VALIDI, 0) AS VAR_CLIENTI_VALIDI,
		ISNULL(C.VAR_CLIENTI_TOTALI, 0) AS VAR_CLIENTI_TOTALI,
		ISNULL(C.VAR_PERCENTUALE, 0) AS VAR_PERC,
		
		ISNULL(C.COMPLIANCE_CLIENTI_TOTALE, 0) AS COMPLIANCE_CLIENTI,
		ISNULL(C.COMPLIANCE_CLIENTI_PROFILATI, 0) AS COMPLIANCE_PROFILATI,
		ISNULL(C.COMPLIANCE_PERCENTUALE, 0) AS COMPLIANCE_PERC,
		ISNULL(C.CONFORMITA_PERCENTUALE, 0) AS CONFORMITA,

		CAST(0 AS NUMERIC) AS FLAG_PB_A_BONUS,
		CASE WHEN ISNULL(O.CATEGORIA_PB, '') IN ('TOP', 'PWA') THEN 7 ELSE A.FLAG_ESCLUSIONE END AS FLAG_ESCLUSIONE,
		A.FLAG_ECCEZIONE
	INTO Classifica.INCENTIVAZIONE_GM_DETTAGLIO 
	FROM
		CTE_AGENTE AS A (NOLOCK)
		LEFT JOIN Classifica.RACCOLTA_TFA_GM_PB_TARGET CAP (NOLOCK) ON	A.CODAGE = CAP.CODAGE 
																		AND CAP.TIPO_PB = 'PB'
		LEFT JOIN RNT.CAPPATURA AS CAP_RNT (NOLOCK) ON	A.CODAGE = CAP_RNT.CODAGE 
														AND CAP_RNT.TIPO_PB = 'PB'
		LEFT JOIN CTE_OBIETTIVO O (NOLOCK) ON	A.CODAGE = O.CODAGE
		LEFT JOIN CONFORMITA.CONFORMITA C (NOLOCK) ON C.CODAGE = A.CODAGE
										AND C.GRADO = '040'
	ORDER BY 
		A.CODAGE50

	ALTER TABLE Classifica.INCENTIVAZIONE_GM_DETTAGLIO ALTER COLUMN CODAGE80 VARCHAR(6) NOT NULL
	ALTER TABLE Classifica.INCENTIVAZIONE_GM_DETTAGLIO ALTER COLUMN CODAGE70 VARCHAR(6) NOT NULL
	ALTER TABLE Classifica.INCENTIVAZIONE_GM_DETTAGLIO ALTER COLUMN CODAGE60 VARCHAR(6) NOT NULL
	ALTER TABLE Classifica.INCENTIVAZIONE_GM_DETTAGLIO ALTER COLUMN CODAGE50 VARCHAR(6) NOT NULL
	ALTER TABLE Classifica.INCENTIVAZIONE_GM_DETTAGLIO ALTER COLUMN CODAGE VARCHAR(6) NOT NULL

	ALTER TABLE Classifica.INCENTIVAZIONE_GM_DETTAGLIO ADD CONSTRAINT PK_INCENTIVAZIONE_GM_DETTAGLIO PRIMARY KEY CLUSTERED 
	(
		CODAGE80,
		CODAGE70,
		CODAGE60,
		CODAGE50,
		CODAGE
	) WITH( STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON) ON [PRIMARY]

	UPDATE A SET
		A.IMP_CAP_RES = B.IMP_CAP,
    	A.IMP_TOTALE_VAL_RES = B.IMP_TOTALE_VAL_RT,
		A.IMP_TOTALE_BVL_RES = B.IMP_TOTALE_BVL_RT,
		A.IMP_TOTALE_RNT_RES = B.IMP_TOTALE_RNT_RT,
		A.IMP_TOTALE_RNG_RES = B.IMP_TOTALE_RNG_RT
    FROM
    	Classifica.INCENTIVAZIONE_GM_DETTAGLIO A INNER JOIN
		Classifica.RACCOLTA_TFA_GM_PB_TARGET B ON	B.CODAGE = A.CODAGE AND
													B.TIPO_PB = A.TIPO_PB

	IF(@Rete = 'SPI')
	BEGIN	
		DELETE
		FROM
			Classifica.INCENTIVAZIONE_GM_DETTAGLIO 
		WHERE
			CODAGE >= '009000'
	END

-- Valorizzo la colonna IMP_RFA	
	UPDATE A SET
		IMP_RFA = B.STORFA_IMPORTO
	FROM
		Classifica.INCENTIVAZIONE_GM_DETTAGLIO A (NOLOCK) INNER JOIN
		NSR_STAGING.dbo.STORFA B (NOLOCK) ON B.STORFA_CODAGE = A.CODAGE
	WHERE 
		B.STORFA_DATA = @DataStorfa 

-- imposto se  pb a bonus o meno
	UPDATE Classifica.INCENTIVAZIONE_GM_DETTAGLIO
		SET FLAG_PB_A_BONUS = 1
	WHERE
		CASE
			WHEN TIPO_PB = 'PB'
			THEN IMP_CAP
			ELSE IMP_CAP_RES
		END >= IMP_OBIETTIVO

-- update per inseire le eccezioni
	UPDATE A SET
		--Gestito il cambio struttura
		A.CODAGE80 = ISNULL(C.CODAGE80, A.CODAGE80),
		A.CODAGE70 = ISNULL(C.CODAGE70, A.CODAGE70),
		A.CODAGE60 = ISNULL(C.CODAGE60, A.CODAGE60),

		A.CODAGE50 = ISNULL(B.CODAGE50, A.CODAGE50),
		A.IMP_RFA = ISNULL(B.IMP_RFA, A.IMP_RFA),
		A.FLAG_ESCLUSIONE = ISNULL(B.FLAG_ESCLUSIONE, A.FLAG_ESCLUSIONE),
		A.FLAG_ECCEZIONE = 1
	FROM
		Classifica.INCENTIVAZIONE_GM_DETTAGLIO A (NOLOCK) INNER JOIN
		dbo.INCENTIVAZIONE_GM_ECCEZIONE B (NOLOCK) ON B.CODAGE = A.CODAGE LEFT JOIN
		(
			SELECT DISTINCT
            	CODAGE50S,
				CODAGE60,
				CODAGE70,
				CODAGE80
            FROM
            	dbo.GERARCHIA_AGENTE_WEB (NOLOCK)
		) C ON C.CODAGE50S = ISNULL(B.CODAGE50, A.CODAGE50)

--creo la tabella dei GM

	IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'Classifica.INCENTIVAZIONE_GM_PREMIO') AND type in (N'U'))
	BEGIN
		DROP TABLE Classifica.INCENTIVAZIONE_GM_PREMIO
	END

	;WITH CTE_OBIETTIVO
	AS
	(
		SELECT
			O.CODAGE,
			O.IMP_OBIETTIVO,
			DO.CATEGORIA_PB
		FROM 
			dbo.OBIETTIVI O (NOLOCK)
			LEFT JOIN dbo.DECODIFICA_OBIETTIVI_RUOLO_RM DO (NOLOCK) ON O.ID_CATEGORIA_PB = DO.ID_CATEGORIA_PB
		WHERE
			O.COD_MISURA = 'RNT'
			AND O.SIGLA_INC = 'C'
	)	
	SELECT
		ID = IDENTITY(INT, 1, 1),
		CODAGE80,
		CODAGE70,
		CODAGE60,
		CODAGE50,

		CAST(0 AS NUMERIC(38, 2)) AS IMP_CAP,
		SUM(IMP_RFA) AS IMP_RFA,
		CAST(0 AS NUMERIC(38, 2)) AS PERC_CRESC_GRUPPO,

		SUM(IMP_TOTALE_RNT_RT_GM) AS IMP_TOTALE_RNT_RT,

		SUM(IMP_TOTALE_VAL_RT_GM) AS IMP_TOTALE_VAL_RT,
		SUM(IMP_TOTALE_BVL_RT_GM) AS IMP_TOTALE_BVL_RT,
		
		SUM(IMP_TOTALE_RNG_RT_GM) AS IMP_TOTALE_RNG_RT,

		CAST(0 AS NUMERIC(38, 2)) AS IMP_RNT_DA_VAL,

		COUNT(*) AS NUMERO_PB,

		SUM(FLAG_PB_A_BONUS) AS NUMERO_PB_A_BONUS,
		CAST(0 AS NUMERIC(5, 2)) AS PERC_PB_A_BONUS,

		SUM(QAV_ATTUALE_CLIENTI) AS QAV_ATTUALE_CLIENTI,
		SUM(QAV_ATTUALE_SINTESI) AS QAV_ATTUALE_SINTESI,
		CAST(0 AS NUMERIC(5, 2)) AS QAV_PERC,

		SUM(VAR_CLIENTI_VALIDI) AS VAR_CLIENTI_VALIDI,
		SUM(VAR_CLIENTI_TOTALI) AS VAR_CLIENTI_TOTALI,
		CAST(0 AS NUMERIC(5, 2)) AS VAR_PERC,

		SUM(COMPLIANCE_CLIENTI) AS COMPLIANCE_CLIENTI,
		SUM(COMPLIANCE_PROFILATI) AS COMPLIANCE_PROFILATI,
		CAST(0 AS NUMERIC(5, 2)) AS COMPLIANCE_PERC,
		CAST(0 AS NUMERIC (5,2)) AS CONFORMITA,

		NSRFrontendWebMultiRete.COMUNE.fn_DISTANZA_TRA_DUE_DATE(a.DATNAS, @DataInizioAnno, 'A') AS ETA_GM,
		CASE WHEN NSRFrontendWebMultiRete.COMUNE.fn_DISTANZA_TRA_DUE_DATE(a.DATNAS, @DataInizioAnno, 'A') > @eta THEN 'GMOLD' ELSE 'GMNEXT' END AS TIPO_GM,
		o.CATEGORIA_PB AS CATEGORIA_GM,

		CAST(0 AS INT) AS FLAG_ESCLUSIONE,

		CAST(0 AS NUMERIC) AS BONUS,
		CAST(0 AS NUMERIC) AS BONUS_EFFETTIVO,
		0 AS FLAG_ECCEZIONE
	INTO Classifica.INCENTIVAZIONE_GM_PREMIO
	FROM
		Classifica.INCENTIVAZIONE_GM_DETTAGLIO dett (NOLOCK)
		INNER JOIN dbo.AGENTE a (NOLOCK) ON	dett.CODAGE50 = a.CODAGE
		LEFT OUTER JOIN CTE_OBIETTIVO o ON	dett.CODAGE50 = o.CODAGE
	WHERE
		FLAG_ESCLUSIONE = 0
	GROUP BY 
		CODAGE80,
		CODAGE70,
		CODAGE60,
		CODAGE50,
		a.DATNAS,
		o.CATEGORIA_PB

-- Calcolo la percentuale QAV E COMPLIANCE	

	UPDATE Classifica.INCENTIVAZIONE_GM_PREMIO
	SET QAV_PERC = CASE 
						WHEN QAV_ATTUALE_CLIENTI = 0
						THEN 0 
						ELSE 
							CASE
								WHEN (CAST(QAV_ATTUALE_SINTESI AS NUMERIC (10, 2)) * 100 / QAV_ATTUALE_CLIENTI) > 100
								THEN 100
								ELSE (CAST(QAV_ATTUALE_SINTESI AS NUMERIC (10, 2)) * 100 / QAV_ATTUALE_CLIENTI)
							END
					END,

		VAR_PERC = CASE 
						WHEN VAR_CLIENTI_TOTALI = 0
						THEN 0 
						ELSE 
							CASE
								WHEN (CAST(VAR_CLIENTI_VALIDI AS NUMERIC (10, 2)) * 100 / VAR_CLIENTI_TOTALI) > 100
								THEN 100
								ELSE (CAST(VAR_CLIENTI_VALIDI AS NUMERIC (10, 2)) * 100 / VAR_CLIENTI_TOTALI)
							END
					END,

		COMPLIANCE_PERC = CASE 
							WHEN COMPLIANCE_CLIENTI = 0
							THEN 0 
							ELSE 
								CASE
									WHEN (CAST(COMPLIANCE_PROFILATI AS NUMERIC (10, 2)) * 100 / COMPLIANCE_CLIENTI) > 100
									THEN 100
									ELSE (CAST(COMPLIANCE_PROFILATI AS NUMERIC (10, 2)) * 100 / COMPLIANCE_CLIENTI)
								END
						 END

--Calcolo minimo per conformita'
	UPDATE Classifica.INCENTIVAZIONE_GM_PREMIO SET
		CONFORMITA = (SELECT MIN(C) FROM (VALUES (COMPLIANCE_PERC), (QAV_PERC), (VAR_PERC)) A (C))

-- Calcolo la percentuale pb a bonus
	UPDATE Classifica.INCENTIVAZIONE_GM_PREMIO SET
		PERC_PB_A_BONUS =	CASE
								WHEN NUMERO_PB = 0
								THEN 0 
								ELSE 
									CASE
										WHEN (NUMERO_PB_A_BONUS * 100 / NUMERO_PB) > 100
										THEN 100
										ELSE (CAST(NUMERO_PB_A_BONUS AS NUMERIC (10, 2)) * 100 / NUMERO_PB) 
									END
							END
	
-- Calcolo cappatura
	DECLARE @CAPPATURA AS dbo.TYPE_CAPPATURA 

	IF (@Rete = 'IWS')
	BEGIN
		INSERT INTO @CAPPATURA 
		(
			Id,
			ImportoMisuraDaCappareTotale,
			ImportoMisuraDaCappare,
			ImportoMisuraConfrontoTotale,
			ImportoMisuraConfronto,
			Coefficiete
		)
		SELECT
			ID,
			IMP_TOTALE_RNT_RT,
			IMP_TOTALE_RNT_RT,
			IMP_TOTALE_RNG_RT,
			IMP_TOTALE_RNG_RT,
			700
		FROM
			Classifica.INCENTIVAZIONE_GM_PREMIO WITH (NOLOCK)
	END
	ELSE
	BEGIN
		INSERT INTO @CAPPATURA 
		(
			Id,
			ImportoMisuraDaCappareTotale,
			ImportoMisuraDaCappare,
			ImportoMisuraConfrontoTotale,
			ImportoMisuraConfronto,
			Coefficiete
		)
		SELECT
			ID,
			IMP_TOTALE_RNT_RT,
			IMP_TOTALE_RNT_RT,
			IMP_TOTALE_VAL_RT + IMP_TOTALE_BVL_RT,
			IMP_TOTALE_VAL_RT + IMP_TOTALE_BVL_RT,
			700
		FROM
			Classifica.INCENTIVAZIONE_GM_PREMIO WITH (NOLOCK)
	END

	UPDATE A SET
		A.IMP_RNT_DA_VAL = B.ImportoMisuradaCappareDaMisuraConfronto,
		A.IMP_CAP = B.ImportoCappato
	FROM
		Classifica.INCENTIVAZIONE_GM_PREMIO A INNER JOIN
		dbo.fn_Cappatura(@CAPPATURA) B ON B.Id = A.ID 
		
-- Aggiorno i valori dei cancelli impostati dall'utente sull'eccezione, ma non forzo il FLAG_ESCLUSIONE 
-- a 0 perchè l'utente non ha valorizzato tale informazione sull'interfaccia
	UPDATE A SET
		A.IMP_RFA = ISNULL(B.IMP_RFA_GM, A.IMP_RFA),
		--A.FLAG_ESCLUSIONE = ISNULL(B.FLAG_ESCLUSIONE_GM, 0),
		A.FLAG_ECCEZIONE = 1
    FROM
    	Classifica.INCENTIVAZIONE_GM_PREMIO A INNER JOIN
		dbo.INCENTIVAZIONE_GM_ECCEZIONE B ON	B.CODAGE50 = A.CODAGE50 AND
												ISNULL(B.CODAGE, '') = '' AND
												B.FLAG_ESCLUSIONE_GM IS NULL

-- Calcolo la percentuale di crescita del gruppo	
	UPDATE INCENTIVAZIONE_GM_PREMIO SET
		PERC_CRESC_GRUPPO = CASE 
								WHEN IMP_RFA = 0
								THEN 0 
								ELSE (CAST(IMP_CAP AS NUMERIC (10, 2)) * 100 / IMP_RFA)
							END

--Verifico il superamento dei cancelli se hanno almeno 5 pb supervisionati e RFA complessiva di quest'ultimi >= 50.000.000
--[RQ03]#2020 •	Almeno 4 PB supervisionati dal GM con RFA del gruppo al 31/12/2019 dei PB supervisionati pari ad almeno € 40 milioni
	UPDATE Classifica.INCENTIVAZIONE_GM_PREMIO SET
		FLAG_ESCLUSIONE = CASE  
							WHEN TIPO_GM = 'OLD' AND NUMERO_PB >= @NumeroPBOld AND IMP_RFA >= @SogliaRFAOld AND ROUND(CONFORMITA, 0) < @PercentualeConformitaOld THEN 2 
							WHEN TIPO_GM = 'OLD' AND NUMERO_PB < @NumeroPBOld OR IMP_RFA < @SogliaRFAOld THEN 1 
							WHEN TIPO_GM = 'NEXT' AND NUMERO_PB >= @NumeroPBNext AND IMP_RFA >= @SogliaRFANext AND ROUND(CONFORMITA, 0) < @PercentualeConformitaNext THEN 2 
							WHEN TIPO_GM = 'NEXT' AND NUMERO_PB < @NumeroPBNext OR IMP_RFA < @SogliaRFANext THEN 1 
							ELSE 0
						  END 

-- Applico le eccezione se FLAG_ESCLUSIONE_GM NON è NULL
	UPDATE A SET
		A.IMP_RFA = ISNULL(B.IMP_RFA_GM, A.IMP_RFA),
		A.FLAG_ESCLUSIONE = B.FLAG_ESCLUSIONE_GM,
		A.FLAG_ECCEZIONE = 1
    FROM
    	Classifica.INCENTIVAZIONE_GM_PREMIO A INNER JOIN
		dbo.INCENTIVAZIONE_GM_ECCEZIONE B ON	B.CODAGE50 = A.CODAGE50 AND
												--ISNULL(B.CODAGE, '') = '' AND
												B.FLAG_ESCLUSIONE_GM IS NOT NULL

--Flag esclusione se il gm non e' piu' gm dopo il 31/03/2018
	UPDATE A SET
		A.FLAG_ESCLUSIONE = 6
	FROM
		Classifica.INCENTIVAZIONE_GM_PREMIO A LEFT JOIN
		dbo.GERARCHIA_AGENTE GA ON GA.CODAGE50 = A.CODAGE50 AND
								   GA.ANNO = @Anno AND
								   GA.TIPO_GERARCHIA = 'RT'
	WHERE GA.CODAGE50 <> A.CODAGE50  

--Flag esclusione dei GM fittizi
	UPDATE A SET
		A.FLAG_ESCLUSIONE = 3
	FROM
		Classifica.INCENTIVAZIONE_GM_PREMIO A 
		INNER JOIN dbo.AGENTE AG ON	AG.CODAGE = A.CODAGE50
									AND AG.DATNAS = 18870101

-- Esclusione dei Next per specifica categoria
	UPDATE A
	SET A.FLAG_ESCLUSIONE = 7
	FROM
		Classifica.INCENTIVAZIONE_GM_PREMIO A 
	WHERE
		A.CATEGORIA_GM IN (SELECT Item FROM NSRFrontendWebMultiRete.COMUNE.fn_SPLIT(@categorieEsclusioneNext, LEFT(@categorieEsclusioneNext, 1)))
		AND A.TIPO_GM = 'NEXT'

	EXEC Classifica.PR_VALORIZZA_BONUS_DA_MATRICE 'Classifica.INCENTIVAZIONE_GM_PREMIO', 'BONUS', 'dbo.BONUS_MATRICE_TRE', 'BONUS', 'GMOLD'
	EXEC Classifica.PR_VALORIZZA_BONUS_DA_MATRICE 'Classifica.INCENTIVAZIONE_GM_PREMIO', 'BONUS', 'dbo.BONUS_MATRICE_TRE', 'BONUS', 'GMNEXT'
	
	UPDATE Classifica.INCENTIVAZIONE_GM_PREMIO SET
		BONUS = CASE 
					WHEN ROUND(CONFORMITA, 0) < @PercentualeConformitaOld THEN 0
					ELSE BONUS
				END,
		BONUS_EFFETTIVO = CASE 
							WHEN ROUND(CONFORMITA, 0) < @PercentualeConformitaOld THEN 0
						  	ELSE BONUS
						  END
	WHERE
		TIPO_GM = 'OLD'

	UPDATE Classifica.INCENTIVAZIONE_GM_PREMIO SET
		BONUS = CASE 
					WHEN ROUND(CONFORMITA, 0) < @PercentualeConformitaNext THEN 0
					ELSE BONUS
				END,
		BONUS_EFFETTIVO = CASE 
							WHEN ROUND(CONFORMITA, 0) < @PercentualeConformitaNext THEN 0
						  	ELSE BONUS
						  END
	WHERE
		TIPO_GM = 'NEXT'
END
GO